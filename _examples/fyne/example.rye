fyne: go\ "fyne.io/fyne/v2"
app: go\ "fyne.io/fyne/v2/app"
widget: go\ "fyne.io/fyne/v2/widget"
dialog: go\ "fyne.io/fyne/v2/dialog"
container: go\ "fyne.io/fyne/v2/container"
time: go\ "time"

a: app/New
w: a .NewWindow "Hello, world!"

w .SetMainMenu fyne/NewMainMenu [
    fyne/NewMenu "File" [ ]
    fyne/NewMenu "Settings" [ ]
]

clock: widget/NewLabel do { time/Now .to-string }
go does {
    forever {
        sleep 1 .seconds

        fyne/Do does {
            clock .SetText time/Now .to-string
        }
    }
}

w .SetContent container/NewBorder
    container/NewVBox [
        do {
            lab: widget/NewLabel "Hi"
            lab .SetText "Hello!"
            lab
        }
        do {
            se: widget/NewSelectEntry [ "A" "B" ]
            se .SetText "A"
            se
        }
        do {
            p: widget/NewProgressBarInfinite
            p .Start
            p
        }
        widget/NewButton "Say hi" does {
            dialog/ShowInformation "Hello!" "You clicked the button" w
        }
        clock
    ]
    nil
    nil
    nil
    [
        widget/NewList
            does { 1000 }
            does { widget/NewLabel "Hi" }
            fn { id lab } { lab .SetText "Entry no. " ++ id .to-string }
    ]

w .ShowAndRun