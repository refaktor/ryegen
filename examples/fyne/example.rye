fyne: import\go "fyne.io/fyne/v2"
app: import\go "fyne.io/fyne/v2/app"
widget: import\go "fyne.io/fyne/v2/widget"
dialog: import\go "fyne.io/fyne/v2/dialog"
container: import\go "fyne.io/fyne/v2/container"
theme: import\go "fyne.io/fyne/v2/theme"
time: import\go "time"

a: app/New
w: a .NewWindow "Hello, world!"

w .SetMainMenu fyne/NewMainMenu [
    fyne/NewMenu "File" [
        do {
            itm:: fyne/NewMenuItem "Open" does { }
            itm .Icon! theme/FileIcon
            itm
        }
    ]
    fyne/NewMenu "Settings" [
        do {
            itm:: fyne/NewMenuItem "Preferences" does { }
            itm .Icon! theme/SettingsIcon
            itm
        }
    ]
]

clock: widget/NewLabel do { time/Now .to-string }
go does {
    forever {
        sleep 1 .seconds

        fyne/Do does {
            clock .SetText time/Now .to-string
        }
    }
}

w .SetContent container/NewBorder
    container/NewVBox [
        do {
            lab: widget/NewLabel "Hi"
            lab .SetText "Hello!"
            lab
        }
        do {
            se: widget/NewSelectEntry [ "A" "B" ]
            se .SetText "A"
            se
        }
        do {
            p: widget/NewProgressBarInfinite
            p .Start
            p
        }
        widget/NewButton "Say hi" does {
            dialog/ShowInformation "Hello!" "You clicked the button" w
        }
        clock
    ]
    nil
    nil
    nil
    [
        widget/NewList
            does { 1000 }
            does { widget/NewLabel "Hi" }
            fn { id lab } { lab .SetText "Entry no. " ++ id .to-string }
    ]

w .ShowAndRun